configurations {
    checkerFrameworkCheckerJar {
        description = 'the Checker Framework, including the Type Annotations compiler'
    }

    checkerFrameworkAnnotatedJDK {
        description = 'a copy of JDK classes with Checker Framework type qualifers inserted'
    }
}

repositories {
    mavenCentral()
}

ext.checkerFrameworkVersion = '2.5.1'

dependencies {
    checkerFrameworkAnnotatedJDK "org.checkerframework:jdk8:${checkerFrameworkVersion}"
    checkerFrameworkCheckerJar "org.checkerframework:checker:${checkerFrameworkVersion}"
}

subprojects {
    apply plugin: 'java'

    dependencies {
        implementation "org.checkerframework:checker-qual:${checkerFrameworkVersion}"
    }

    tasks.withType(JavaCompile).all { JavaCompile javaCompile ->
        javaCompile.doFirst {
            javaCompile.options.compilerArgs = [
                    '-processorpath', "${rootProject.configurations.checkerFrameworkCheckerJar.asPath}",
                    '-processor', 'org.checkerframework.checker.nullness.NullnessChecker,org.checkerframework.checker.formatter.FormatterChecker',
                    '-Xmaxerrs', '10000',
                    '-Xmaxwarns', '10000',
                    '-AcheckPurityAnnotations',
                    "-Xbootclasspath/p:${rootProject.configurations.checkerFrameworkAnnotatedJDK.asPath}"
            ]
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}
