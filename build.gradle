buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'nu.studer:gradle-jooq-plugin:1.0.5'
        classpath group: 'com.h2database', name: 'h2', version: '1.4.192'
    }
}


plugins {
    id "org.flywaydb.flyway" version "4.0.3"
}

group 'me.loki2302'
version '1.0-SNAPSHOT'

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'application'
apply plugin: 'nu.studer.jooq'

sourceCompatibility = 1.8
mainClassName = 'me.loki2302.App'

repositories {
    mavenCentral()
    maven { url 'http://jasperreports.sourceforge.net/maven2/' }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile group: 'net.sourceforge.dynamicreports', name: 'dynamicreports-core', version: '4.1.1'
    compile group: 'net.sourceforge.dynamicreports', name: 'dynamicreports-adhoc', version: '4.1.1'
    compile group: 'net.sourceforge.dynamicreports', name: 'dynamicreports-googlecharts', version: '4.1.1'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.10'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jooq', version: '1.4.0.RELEASE'
    compile group: 'com.h2database', name: 'h2', version: '1.4.192'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '4.0.3'
}

flyway {
    driver = 'org.h2.Driver'
    url = 'jdbc:h2:./dbsample/dummy'
    user = 'SA'
}

jooq {
    dummy(sourceSets.main) {
        jdbc {
            driver = 'org.h2.Driver'
            url = 'jdbc:h2:./dbsample/dummy'
            user = 'SA'
        }

        generator {
            database {
                name = 'org.jooq.util.h2.H2Database'
                inputSchema = 'PUBLIC'
            }
            target {
                packageName = 'me.loki2302.db'
                directory = 'src/generated/jooq'
            }
        }
    }
}
